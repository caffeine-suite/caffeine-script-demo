import &ArtSuite

class App extends PointerActionsMixin Component

  @stateFields
    slides:             slides = array &Slides.modules
    currentSlideIndex:  0

  @getter
    currentSlide: -> @slides[@currentSlideIndex %% @slides.length]

  previousSlide:  -> @currentSlideIndex -= 1
  nextSlide:      -> @currentSlideIndex += 1

  render: ->
    CanvasElement

      RectangleElement
        color:        :black

      Element
        key:        @currentSlide.getName()
        # cacheDraw:  true
        animators:
          opacity:  toFrom: 0
          axis:     toFrom: y: -.5, x: 0

        Element
          axis:         :centerCenter
          location: ps: :centerCenter
          size:         w: 1920, h: 1080
          scale:        (ps, cs) -> ps.div(cs).min()

          @currentSlide()

      Element
        receivePointerEvents: :passToChildren
        childrenLayout: :row
        size:           cs: 1
        location:       yh: 1
        axis:           :bottomLeft
        padding:        5
        on:             @hoverHandlers
        animators:      opacity: true
        opacity:        if @hover then 1 else .5

        &Button text: "previous",  action: @previousSlide

        Element receivePointerEvents: :never

        &Button text: "next",      action: @nextSlide
